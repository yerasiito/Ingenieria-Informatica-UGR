### A Pluto.jl notebook ###
# v0.19.25

using Markdown
using InteractiveUtils

# This Pluto notebook uses @bind for interactivity. When running this notebook outside of Pluto, the following 'mock version' of @bind gives bound variables a default value (instead of an error).
macro bind(def, element)
    quote
        local iv = try Base.loaded_modules[Base.PkgId(Base.UUID("6e696c72-6542-2067-7265-42206c756150"), "AbstractPlutoDingetjes")].Bonds.initial_value catch; b -> missing; end
        local el = $(esc(element))
        global $(esc(def)) = Core.applicable(Base.get, el) ? Base.get(el) : iv(el)
        el
    end
end

# ‚ïî‚ïê‚ï° 5ccf83de-10cd-46e0-b940-664b67831e48
begin
	using Pkg
	Pkg.activate(".")
	using PlutoUI
	import Distances
	using Plots
	using Random: shuffle, seed!
	using PlutoTeachingTools
	using DataFrames
	PlutoTeachingTools.set_language!(PlutoTeachingTools.get_language_from_env())
	PlutoUI.TableOfContents(title="üìö √çndice", indent=true)
end

# ‚ïî‚ïê‚ï° 0e1d1860-943a-4808-834f-833e15bc0396
html"""<style>
main {
    max-width: 1000px;
}
"""

# ‚ïî‚ïê‚ï° f78d4556-c30e-11ed-3cc3-ffa49c1c0da9

md"# Resolviendo el TSP con Metaheur√≠sticas, Parte 1"

# ‚ïî‚ïê‚ï° 63967850-2682-4d70-89cf-76d91d5da628
md"""
## Sobre este documento

Este documento forma parte del **Proyecto de Innovaci√≥n Docente Desarrollo de herramientas web interactivas para el aprendizaje de Metaheur√≠sticas** asociado a la asignatura de Metaheur√≠sticas que se imparte para el _Grado en Ingenier√≠a Inform√°tica_ (aunque abierta a grados dobles) en la **Universidad de Granada (UGR)**.
"""

# ‚ïî‚ïê‚ï° 8ff8d174-7651-493a-94ba-c613fd7ff69b
md"""
En este sistema interactivo se mostrar√° c√≥mo se puede aplicar un problema concreto, el _Viajante de Comercio_ para resolverlo con distintas metaheur√≠sticas, algo semejante a lo que tienen que hacer los y las estudiantes de la asignatura, pero con problemas distintos. Al igual que las pr√°cticas de la asignatura, este entorno web est√° dividido en tres partes, cada una de ellas abordando distintas metaheur√≠sticas sobre el mismo problema.
"""

# ‚ïî‚ïê‚ï° 4b016a19-3e60-429c-a52b-9a7415bbb808
md"""
## Instrucciones

Este documento est√° en formato [Pluto](https://plutojl.org/) para ofrecer un entorno reactivo, lo cual quiere decir que se pueden pulsar botones o cambiar valores usando elementos visuales (como Select, sliders, ...) y el sistema se
actualiza.

El c√≥digo fuente disponible est√° en el lenguaje de programaci√≥n [Julia](https://julialang.org/), que es muy similar a Python. Lo he escogido porque es muy legible, similar al seudoc√≥digo pero al mismo tiempo es ejecutable. Se puede seleccionar *Edit in binder* y se podr√° editar en una copia local.

!!! warning

	Aunque Julia es muy similar a Python hay algunas diferencias, comentar√© las m√°s notables: 
    - Los vectores se acceden desde la posici√≥n 1 hasta tam, en vez de desde la posici√≥n 0 hasta tam-1 (siendo tam el  tama√±o del vector). 
    - Los rangos se ponen como 1:N, ambos inclu√≠dos, y no range(1, N+1) como en Python.
    - Julia ya tiene soporte para vectores y matrices, no es necesario usar una librer√≠a como _numpy_ en Python.
	- Dentro de una cadena se puede usar $ para mostrar los valores de una variable (similar al fmt de Python).

	Hay recursos _online_ para ver ciertas [diferencias entre Python y Julia](https://towardsdatascience.com/moving-code-from-python-to-julia-beware-of-false-friends-160573a5d552), pero no es necesario profundizar para entender los algoritmos y "_jugar_", si se quiere, con el c√≥digo. 
"""

# ‚ïî‚ïê‚ï° d09d69b4-16b6-4bc4-ac73-209d4599fcce
TwoColumnWideLeft(
md"""
## El problema del Viajante de Comercio

El problema del Viajante de Comercio (_Travelman Salesman Problem_, TSP) es un problema conocido de la literatura, en el que existe un viajante que debe de recorrer una serie de ciudades y desea encontrar la mejor ruta para ello. Por tanto, el problema consiste en:

- Encontrar la ruta m√°s corta entre todas las ciudades.
- Pasar por cada ciudad √∫nicamente una vez.
- Volver a la ciudad origen.

El objetivo es encontrar la asignaci√≥n $S$ que minimiza:
```math
\displaystyle \sum_{i=0}^{N-1}Distancia({S_i,S_{i+1}}) + Distancia({S_N,S_0})
```
""",RobustLocalResource("https://images.danimolina.net/TSP.jpg", "TSP.jpg"))

# ‚ïî‚ïê‚ï° 13528697-092b-43f8-b6e3-12e4cbc12559
begin
	function description(fname)
		open(fname) do file
			line = readline(file)
			(attrib, val) = split(line, ":") .|> strip
			
			while (attrib != "COMMENT")
				line = readline(file)
				(attrib, val) = split(line, ":") .|> strip
			end
			return val |> strip
		end
	end
	fnames = filter(endswith("tsp"), readdir("data", join=true))
	md"""
Para abordar el problema usaremos un conjunto de ciudades de TSP sacadas del [TSPlib](http://comopt.ifi.uni-heidelberg.de/software/TSPLIB95/)

Escoge un problema: 
$(@bind fname Select([name => description(name) for name in fnames], default=fnames[5]))
"""
end

# ‚ïî‚ïê‚ï° 37c8cf4b-eed8-428f-b181-36727af9d472
begin
	function read_TSP(fname)
		@assert isfile(fname)
	
		open(fname) do file
			line = readline(file)
			N = 0

			while (contains(line, ":"))
				(atrib,val) = split(line, ":") .|> strip
				if atrib == "DIMENSION"
					N = parse(Int, val)
				end
				line = readline(file) |> strip
			end
			@assert line == "NODE_COORD_SECTION"
			local distances = zeros(Float32, N, N)
			local position_cities = Matrix{Float32}(undef, 2, N)
			
			for i in 1:N
				line = readline(file)
				(_, x, y) = split(line, " ", keepempty=false)
				x, y = parse.(Ref(Float32), (x, y))
				position_cities[1,i] = x
				position_cities[2,i] = y
			end
			#posi = transpose(position_cities)
			distances = Distances.pairwise(Distances.Euclidean(), position_cities, dims=2)
			return N, position_cities, distances
		end
	end
end

# ‚ïî‚ïê‚ï° dbb838ef-262a-4db4-8bae-5e8332fba3a6
md"Vamos a leer sus datos (suponiendo que existe la fuci√≥n **read_TSP** que devuelve el tama√±o, las posiciones (para pintar visualmente) y las distancias entre ellas (calculadas como las distancias eucl√≠deas a partir de sus posiciones):"

# ‚ïî‚ïê‚ï° 02fcaf7d-7fe5-48f6-83ba-5387e342892d
N, Position_cities, Distances_cities = read_TSP(fname);

# ‚ïî‚ïê‚ï° d80dd12d-c9d8-4dc1-b42d-ecb746b98101
md"Vamos a visualizar el problema:"

# ‚ïî‚ïê‚ï° 80eef61f-1414-4aeb-86de-416263797d29
begin
	function plot_TSP(position_cities, options=Dict{}())
		scatter(position_cities[1,:], position_cities[2,:]; title=description(fname), legend=false, axis=false, options...)
	end
	plot_TSP(Position_cities)
end

# ‚ïî‚ïê‚ï° 3315e650-cf41-4bd9-96f1-e548a19f7f44
md"""
## Representaci√≥n del problema

Por comodidad usaremos como representaci√≥n del problema el orden en el que las ciudades son recorridas. Y supondremos que vuelve de la √∫ltima ciudad a la primera.

Esto implica que toda permutaci√≥n entre 1 y $(N) ser√° una soluci√≥n v√°lida para el problema.

Por ejemplo, si tenemos la soluci√≥n aleatoria: $(@bind go Button(\"Genera soluci√≥n\"))
"""

# ‚ïî‚ïê‚ï° 251fbc45-c637-4652-ad50-01d40ce2537c
begin
	go
	sol = shuffle(1:N)
end

# ‚ïî‚ïê‚ï° 88d163e8-1f21-48a4-9ae6-2bb217b7cff3
md"Visualmente ser√≠a"

# ‚ïî‚ïê‚ï° 66183a3b-11e7-4c92-8784-714b5d22738e
begin
	function plot_sol(positions, sol, fit_sol=missing)
		if ismissing(fit_sol)
			title = "Solution to $(description(fname))"
		else
			title = "Fitness: $(round(fit_sol, digits=2))"
		end
		plt = plot_TSP(positions, Dict(:markersize=>3, :color=>:black, :title=>title))
		for i in 1:(length(sol)-1)
			ant = sol[i]
			next = sol[i+1]
			plot!(plt, positions[1,[ant,next]], positions[2,[ant,next]], color=:gray, style=:dash, linealpha=0.5)
		end
		plot!(plt, positions[1,[sol[end],sol[begin]]], positions[2,[sol[end],sol[begin]]], color=:gray, style=:dash, linealpha=0.5)
		plt
	end

	plot_sol(Position_cities, sol)
end

# ‚ïî‚ïê‚ï° 0daabc72-3ef2-488d-8f6e-aefd2cae841d
md"""
## Evaluando una soluci√≥n

En este apartado vamos a ver c√≥mo podemos evaluar una soluci√≥n. 

Para ello, definiremos una funci√≥n que reciba las distancias y la soluci√≥n, y haga todas las sumas.
"""

# ‚ïî‚ïê‚ï° f6d62ede-1e5e-41e1-9981-909f070d63fb
function fitness(distances, sol)
	total = 0.0
	tam = length(sol)

	for i in 1:(tam-1)
		total += distances[sol[i],sol[i+1]]
	end
	total += distances[sol[tam],sol[1]]
	return total
end

# ‚ïî‚ïê‚ï° 9aa54296-488d-4808-b457-a64ab8fd566c
md"Lo probamos:"

# ‚ïî‚ïê‚ï° 2be1750b-85d5-4b98-a4f0-4148285cb058
fitness(Distances_cities, sol)

# ‚ïî‚ïê‚ï° 668baf12-9a75-4ec6-903e-236ab69066a5
md"Ahora vamos a a√±adirlo a la visualizaci√≥n:"

# ‚ïî‚ïê‚ï° 1792f776-9916-402f-9de3-5716ed6744e9
plot_sol(Position_cities, sol, fitness(Distances_cities, sol))

# ‚ïî‚ïê‚ï° ec6be0aa-e367-49cf-a3d4-cfc1e69602e0
md"""
## Generando soluciones de forma aleatoria

Como valor de referencia vamos primero a intentar obtener la soluci√≥n generando soluciones de forma totalmente aleatoria, y luego compararemos con el resto de algoritmos.
"""

# ‚ïî‚ïê‚ï° 87f6103e-be1f-404b-ab36-7ae68231ebed
md"El primer paso es indicar el n√∫mero de evaluaciones (_maxevals_) que queremos: $(@bind maxevals NumberField(100:50000, default=1000))"

# ‚ïî‚ïê‚ï° efdb8e91-8c36-4990-a18a-be37572de341
md"El c√≥digo para generar _maxevals_ soluciones aleatorias es el siguiente"

# ‚ïî‚ïê‚ï° 498c1ce3-ef85-4472-b568-d7f95710c1f5
md"En donde *inicia_sol* se crea de forma totalmente aleatoria"

# ‚ïî‚ïê‚ï° b810106c-6b18-4670-94e9-976cf56c2ab5
function inicia_sol(n)
	return shuffle(1:n)
end

# ‚ïî‚ïê‚ï° 55c3e565-c98d-4235-a503-2a3fc6f2ef9a
function optim_random(distancias, maxevals)
	n = size(distancias, 1)
	best = inicia_sol(n)
	best_fit = fitness(distancias, best)
	evals = 1

	while evals < maxevals
		newsol = inicia_sol(n)
		fit_new = fitness(distancias, newsol)

		if fit_new < best_fit
			best = newsol
			best_fit = fit_new
		end
		evals += 1
	end

	return best, best_fit
end

# ‚ïî‚ïê‚ï° a0ea972b-1cbf-4f52-a6cc-20f0a66700b2
md"Vamos a probarlo"

# ‚ïî‚ïê‚ï° 8fb74b55-4a82-4eb7-bdcf-d63fa4cf3be4
begin
	best_random, fit_random = optim_random(Distances_cities, maxevals)
	md"La mejor soluci√≥n con pruebas aleatorias da $(round(fit_random, digits=2)) y visualmente es:"
end

# ‚ïî‚ïê‚ï° 9496e22b-913c-4d77-ba36-c3f2ada64a71
plot_sol(Position_cities, best_random, fit_random)

# ‚ïî‚ïê‚ï° 00984827-fc5a-42b5-8073-5d6d7e3e4d54
md"""
!!! note
	¬øC√≥mo os parece? ¬øRazonable?

	Prueba a ajustar el n√∫mero de evaluaciones y ver c√≥mo cambia el fitness.
"""

# ‚ïî‚ïê‚ï° 5bc1c907-f644-4070-9d69-cb27c7864a84
md"""
#### Vamos a ver c√≥mo evoluciona de forma m√°s interactiva.

Usa el _slider_ para adaptar la evaluaci√≥n.
"""

# ‚ïî‚ïê‚ï° b21b5893-36c8-4b30-aaa0-4570a200874f
begin
	slider = @bind evals_dynamic Slider(1:10_000, default=1)
	nothing
end

# ‚ïî‚ïê‚ï° 8b7885ec-c853-432a-89c5-7221ba5eda03
begin
	local sol, fit
	seed!(169)
	sol, fit = optim_random(Distances_cities, evals_dynamic)
	TwoColumnWideRight(md"Evaluaciones: $(evals_dynamic) $br $slider", plot_sol(Position_cities, sol, fit))
end

# ‚ïî‚ïê‚ï° d43f52c0-2b49-49f5-8f5f-139963384599
md"""
### Midiendo el rendimiento

Ahora vamos a medir c√≥mo de r√°pido es el algoritmo. Para ello vamos a probar los tiempos con distinto n√∫mero m√°ximo de evaluaciones (_maxevals_) y ver c√≥mo funciona.
"""

# ‚ïî‚ïê‚ï° a0fe8446-788a-45d9-9a79-8417eba14755
begin
	# Recorro desde 1000 hasta 50000
	evals_time = collect(1_000:1000:50_000)
	local time_alg = Float64[]

	for evals in evals_time
		time = @elapsed optim_random(Distances_cities, evals)
		push!(time_alg, time)
	end

	plot(evals_time, time_alg, legend=false, title="Tiempo con B√∫squeda Aleatoria", xlabel="Evaluaciones", ylabel="Tiempo")
end

# ‚ïî‚ïê‚ï° da349ffa-0e14-470e-9841-8c385b23440c
md"""
¬øC√≥mo crees que se incrementa el tiempo con las evaluaciones?
$(@bind como_increm PlutoUI.MultiCheckBox(["Lineal", "Exponencial"]))
""" |> question_box

# ‚ïî‚ïê‚ï° 2442ec93-4707-42ab-81cb-5d2d68e1643f
if como_increm == ["Lineal"]
	PlutoTeachingTools.correct()
elseif !isempty(como_increm)
	keep_working()
end

# ‚ïî‚ïê‚ï° 4488cb35-342e-418d-93b1-31359baa46e7
md"""
### Analizando la convergencia

Ahora vamos a analizar la convergencia, para ello vamos a guardar la mejor soluci√≥n actual.
"""

# ‚ïî‚ïê‚ï° fcfdcda5-377d-499f-a2bb-77fb72fe03ee
function optim_random_conv(distancias, maxevals, historic)
	n = size(distancias, 1)
	best = inicia_sol(n)
	best_fit = fitness(distancias, best)
	push!(historic, best_fit)
	evals = 1

	while evals < maxevals
		newsol = inicia_sol(n)
		fit_new = fitness(distancias, newsol)

		if fit_new < best_fit
			best = newsol
			best_fit = fit_new
		end
		evals += 1
		# Only change
		push!(historic, best_fit)
	end

	return best, best_fit
end

# ‚ïî‚ïê‚ï° d086c073-9d82-4bfc-87f1-70631c4be183
begin
	local sol, fit, maxevals, historic
	seed!(169)
	maxevals = 50_000
	historic = Float32[]
	sol, fit = optim_random_conv(Distances_cities, maxevals, historic)
	plot(1:maxevals, historic, legend=false, title="Gr√°fica de convergencia", yaxis=:log, xlabel="EValuaciones", ylabel="Mejor Fitness")
end

# ‚ïî‚ïê‚ï° 7499f1ae-c9e7-4426-9c47-efa150ef9f78
Foldable("Comentario", md"""
!!! note

	Como puedes ver, cada vez mejora menos.
""")

# ‚ïî‚ïê‚ï° 3d6186cc-85b5-4539-a7d1-8ee3bf95056c
md"""
## Algoritmo Greedy

Vamos a aplicar el algoritmo Greedy, que recuerdo que se basa en ir construyendo una soluci√≥n paso a paso, escogiendo en cada paso un criterio heur√≠stico para elegir la mejor opci√≥n local, que puede no ser la mejor globalmente. 

En este problema es bastante intuitivo, la heur√≠stica ser√° coger en cada ciudad la siguiente m√°s cercana. 
"""

# ‚ïî‚ïê‚ï° 9d9882ef-aab5-412e-837d-cc3decf51f7f
function greedy(distances)
	N = size(distances, 1)
	sol = zeros(Int, N)
	
	tam = 1
	# Primera aleatoria
	sol[tam] = rand(1:N)
	last = sol[tam]

	while tam < N
		# Miro las pendientes
		to_select = setdiff(1:N, sol)
		dist = -1
		mindist = Inf
		selected = 0
		
		for option in to_select
			dist = distances[sol[tam], option]

			if dist < mindist
				mindist = dist
				selected = option
			end
		end
		tam += 1
		sol[tam] = selected
	end
	
	return sol, fitness(distances, sol)
end

# ‚ïî‚ïê‚ï° dee04e5c-41f5-437d-b2e6-bb3c125cbc40
begin
	sol_greedy, fit_greedy = greedy(Distances_cities)
	md"El greedy da un fitness de $(fit_greedy), ¬øte parece competitivo?"
end

# ‚ïî‚ïê‚ï° 8ee7b911-30de-4d5c-9752-49fa88b63e7f
md"Veamos la evoluci√≥n"

# ‚ïî‚ïê‚ï° e1c68742-621f-48a4-86e6-eda7d2989ae4
begin
	function plot_greedy(positions, distances, sol, tope)
		plt = plot_TSP(positions, Dict(:ms=>3, :color=>:gray))

		for i in 1:(tope-1)
			ant = sol[i]
			next = sol[i+1]
			plot!(plt, positions[1,[ant,next]],positions[2,[ant,next]], color=:black)
		end
	end
	
	anim = @animate for i ‚àà 1:N
    plot_greedy(Position_cities, Distances_cities, sol_greedy, i)
	end
	gif(anim, "greedy.gif", fps = 1)
end

# ‚ïî‚ïê‚ï° 4b6b92d3-a611-49c3-880c-f46fba0cecea
md"""
## Algoritmo de B√∫squeda Local

En este caso vamos a aplicar el modelo de B√∫squeda Local visto en clase, en particular el enfoque primero mejor.

Primero vamos a crear una soluci√≥n aleatoria, y luego aplicamos un operador que cambie la soluci√≥n un poco, la comparamos y nos quedamos la mejor de ambas, y luego se vuelve a aplicar lo mismo, hasta alcanzar un n√∫mero de soluciones.
"""

# ‚ïî‚ïê‚ï° f379ac50-dd36-40ed-b76d-2e5786f2ebc8
md"Definimos la funci√≥n que muta:"

# ‚ïî‚ïê‚ï° e2df8887-5ba4-430c-aad7-5243c2f137ed
function mutate(solution)
	result = copy(solution)
	N = length(solution)
	posi1 = rand(1:N)
	posi2 = rand(1:N)

	# Compruebo que sea diferente de la primera
	while posi2 == posi1
		posi2 = rand(1:N)
	end

	# Intercambio posiciones
	result[posi1] = solution[posi2]
	result[posi2] = solution[posi1]
	return result
end

# ‚ïî‚ïê‚ï° b8f04454-777c-453e-9afa-4e28967c9bb7
md"Ahora definimos el m√©todo de B√∫squeda Local. Para estudiar la convergencia vamos a guardar directamente las soluciones generadas."

# ‚ïî‚ïê‚ï° 3dd5ed16-9e75-4419-8050-7a826b0f8dfa
function busquedaLocal(dist, maxevals=50_000, historic=nothing)
	current_sol = inicia_sol(size(dist, 1))
	current_fit = fitness(dist, current_sol)
	best_sol = copy(current_sol)
	best_fit = current_fit
	evals = 1
	if !isnothing(historic)
		push!(historic, (current_fit, best_fit))
	end
	
	while evals < maxevals
		current_sol = mutate(best_sol)
		current_fit = fitness(dist, current_sol)
		evals += 1

		if current_fit < best_fit
			best_fit = current_fit
			best_sol = current_sol
		end
		
		if !isnothing(historic)
			push!(historic, (current_fit, best_fit))
		end
	end
	
	return best_sol, best_fit
end

# ‚ïî‚ïê‚ï° ee41d7e7-cc0e-4201-95c8-5a631423ec85
md"Se puede usar simplemente usando la funci√≥n **busquedaLocal**"

# ‚ïî‚ïê‚ï° 868f6a4f-1ab4-45eb-beb8-340183faf526
begin
	sol_ls, fit_ls = busquedaLocal(Distances_cities)
	"Fitness: $(fit_ls)"
end

# ‚ïî‚ïê‚ï° c75a3c0a-8d68-4b20-a20b-914ad237972b
md"Vamos a probarlo con un slider"

# ‚ïî‚ïê‚ï° 6628b477-f3f3-451f-81db-cd201a46b17b
begin
	slider_ls = @bind evals_ls Slider(1:50_000, default=1)
	nothing
end

# ‚ïî‚ïê‚ï° 7faa1882-e5dc-4c55-ace5-c0007ed39de1
begin
	local sol, fit
	seed!(169)
	sol, fit = busquedaLocal(Distances_cities, evals_ls)
	TwoColumnWideRight(md"Evaluaciones: $evals_ls $br $slider_ls", plot_sol(Position_cities, sol, fit))
end

# ‚ïî‚ïê‚ï° 2a077552-7ef0-4e53-bfbb-908ebee11d53
begin
function show_conv(method, distances)
	historic = Tuple{Float64,Float64}[]
	sol, fit = method(distances, evals_ls, historic)
	plt = plot(1:evals_ls, first.(historic), label="actual", size=(700, 400), xlabel="Evaluaciones", ylabel="Fitness", yscale=:log10)
	plot!(plt, 1:evals_ls, last.(historic), label="mejor")
end
md"""
### Gr√°fica de convergencia

Vamos a analizar la gr√°fica de convergencia, mostrando tanto el fitness de la mejor soluci√≥n y la soluci√≥n actual
"""
end

# ‚ïî‚ïê‚ï° e7832961-cb91-4dfb-9b2e-e20e617568a6
show_conv(busquedaLocal, Distances_cities)

# ‚ïî‚ïê‚ï° b662a059-b970-4aa4-9fd2-e5a27f0b047f
md"""
## Tabla comparativa

Vamos a medir los algoritmos con distintas evaluaciones y medir los tiempos.
"""

# ‚ïî‚ïê‚ï° 64d92285-f7db-4fa2-ab12-d3b247ffd641
md"""
$(@bind evals_comp Slider(1:100_000, default=1))
"""

# ‚ïî‚ïê‚ï° 46596778-9dbd-426c-a2a7-4014b8b2987c
begin
function times(distances)
	df = DataFrame(name=String[], tiempos=Float32[], best=Float32[])
	seed!(169)

	time_greedy = @elapsed _, fit_greedy = greedy(distances)
	push!(df, (name="Greedy", tiempos=time_greedy, best=fit_greedy))
	seed!(169)
	time_random = @elapsed _, fit_random = optim_random(distances, evals_comp)
	push!(df, (name="Random", tiempos=time_random, best=fit_random))
	seed!(169)
	time_BL = @elapsed _, fit_BL = busquedaLocal(distances, evals_comp)
	push!(df, (name="B√∫squeda Local", tiempos=time_BL, best=fit_BL))
	
	PrettyTables.pretty_table(HTML, df, header=["Algoritmo", "Tiempo", "Mejor Fit."], title="Resultado con $(evals_comp) evaluaciones", formatters = (PrettyTables.ft_printf("%.3e", [2]),PrettyTables.ft_printf("%2.3f", [3])))
end
	times(Distances_cities)
end

# ‚ïî‚ïê‚ï° 086a03d5-aced-4ed5-a250-21ef3c51caeb
PlutoTeachingTools.Foldable(
	"Preguntas", 
	question_box(md"""
	¬øQu√© te parecen los resultados? Indica lo que te parece correcto:

	$(@bind pregunta PlutoUI.MultiCheckBox(["T" => "La b√∫squeda local mejora a la aleatoria", "F" => "La b√∫squeda local no mejora a la aleatoria"]))
	"""
	)
)

# ‚ïî‚ïê‚ï° 6c85c8c1-8758-42ee-99a4-bfb81c70a966
begin
	if !isempty(pregunta) && ("F" in pregunta || length(pregunta)<1)
		keep_working()
	elseif !isempty(pregunta)
		correct()
	end
end

# ‚ïî‚ïê‚ï° Cell order:
# ‚ïü‚îÄ5ccf83de-10cd-46e0-b940-664b67831e48
# ‚ïü‚îÄ0e1d1860-943a-4808-834f-833e15bc0396
# ‚ïü‚îÄf78d4556-c30e-11ed-3cc3-ffa49c1c0da9
# ‚ïü‚îÄ63967850-2682-4d70-89cf-76d91d5da628
# ‚ïü‚îÄ8ff8d174-7651-493a-94ba-c613fd7ff69b
# ‚ïü‚îÄ4b016a19-3e60-429c-a52b-9a7415bbb808
# ‚ïü‚îÄd09d69b4-16b6-4bc4-ac73-209d4599fcce
# ‚ïü‚îÄ13528697-092b-43f8-b6e3-12e4cbc12559
# ‚ïü‚îÄ37c8cf4b-eed8-428f-b181-36727af9d472
# ‚ïü‚îÄdbb838ef-262a-4db4-8bae-5e8332fba3a6
# ‚ï†‚ïê02fcaf7d-7fe5-48f6-83ba-5387e342892d
# ‚ïü‚îÄd80dd12d-c9d8-4dc1-b42d-ecb746b98101
# ‚ïü‚îÄ80eef61f-1414-4aeb-86de-416263797d29
# ‚ïü‚îÄ3315e650-cf41-4bd9-96f1-e548a19f7f44
# ‚ïü‚îÄ251fbc45-c637-4652-ad50-01d40ce2537c
# ‚ïü‚îÄ88d163e8-1f21-48a4-9ae6-2bb217b7cff3
# ‚ïü‚îÄ66183a3b-11e7-4c92-8784-714b5d22738e
# ‚ïü‚îÄ0daabc72-3ef2-488d-8f6e-aefd2cae841d
# ‚ï†‚ïêf6d62ede-1e5e-41e1-9981-909f070d63fb
# ‚ïü‚îÄ9aa54296-488d-4808-b457-a64ab8fd566c
# ‚ï†‚ïê2be1750b-85d5-4b98-a4f0-4148285cb058
# ‚ïü‚îÄ668baf12-9a75-4ec6-903e-236ab69066a5
# ‚ï†‚ïê1792f776-9916-402f-9de3-5716ed6744e9
# ‚ïü‚îÄec6be0aa-e367-49cf-a3d4-cfc1e69602e0
# ‚ïü‚îÄ87f6103e-be1f-404b-ab36-7ae68231ebed
# ‚ïü‚îÄefdb8e91-8c36-4990-a18a-be37572de341
# ‚ï†‚ïê55c3e565-c98d-4235-a503-2a3fc6f2ef9a
# ‚ïü‚îÄ498c1ce3-ef85-4472-b568-d7f95710c1f5
# ‚ïü‚îÄb810106c-6b18-4670-94e9-976cf56c2ab5
# ‚ïü‚îÄa0ea972b-1cbf-4f52-a6cc-20f0a66700b2
# ‚ïü‚îÄ8fb74b55-4a82-4eb7-bdcf-d63fa4cf3be4
# ‚ïü‚îÄ9496e22b-913c-4d77-ba36-c3f2ada64a71
# ‚ïü‚îÄ00984827-fc5a-42b5-8073-5d6d7e3e4d54
# ‚ïü‚îÄ5bc1c907-f644-4070-9d69-cb27c7864a84
# ‚ïü‚îÄb21b5893-36c8-4b30-aaa0-4570a200874f
# ‚ïü‚îÄ8b7885ec-c853-432a-89c5-7221ba5eda03
# ‚ïü‚îÄd43f52c0-2b49-49f5-8f5f-139963384599
# ‚ï†‚ïêa0fe8446-788a-45d9-9a79-8417eba14755
# ‚ïü‚îÄda349ffa-0e14-470e-9841-8c385b23440c
# ‚ïü‚îÄ2442ec93-4707-42ab-81cb-5d2d68e1643f
# ‚ïü‚îÄ4488cb35-342e-418d-93b1-31359baa46e7
# ‚ï†‚ïêfcfdcda5-377d-499f-a2bb-77fb72fe03ee
# ‚ï†‚ïêd086c073-9d82-4bfc-87f1-70631c4be183
# ‚ïü‚îÄ7499f1ae-c9e7-4426-9c47-efa150ef9f78
# ‚ïü‚îÄ3d6186cc-85b5-4539-a7d1-8ee3bf95056c
# ‚ï†‚ïê9d9882ef-aab5-412e-837d-cc3decf51f7f
# ‚ï†‚ïêdee04e5c-41f5-437d-b2e6-bb3c125cbc40
# ‚ïü‚îÄ8ee7b911-30de-4d5c-9752-49fa88b63e7f
# ‚ïü‚îÄe1c68742-621f-48a4-86e6-eda7d2989ae4
# ‚ïü‚îÄ4b6b92d3-a611-49c3-880c-f46fba0cecea
# ‚ïü‚îÄf379ac50-dd36-40ed-b76d-2e5786f2ebc8
# ‚ï†‚ïêe2df8887-5ba4-430c-aad7-5243c2f137ed
# ‚ïü‚îÄb8f04454-777c-453e-9afa-4e28967c9bb7
# ‚ï†‚ïê3dd5ed16-9e75-4419-8050-7a826b0f8dfa
# ‚ïü‚îÄee41d7e7-cc0e-4201-95c8-5a631423ec85
# ‚ïü‚îÄ868f6a4f-1ab4-45eb-beb8-340183faf526
# ‚ïü‚îÄc75a3c0a-8d68-4b20-a20b-914ad237972b
# ‚ïü‚îÄ6628b477-f3f3-451f-81db-cd201a46b17b
# ‚ïü‚îÄ7faa1882-e5dc-4c55-ace5-c0007ed39de1
# ‚ïü‚îÄ2a077552-7ef0-4e53-bfbb-908ebee11d53
# ‚ïü‚îÄe7832961-cb91-4dfb-9b2e-e20e617568a6
# ‚ïü‚îÄb662a059-b970-4aa4-9fd2-e5a27f0b047f
# ‚ïü‚îÄ64d92285-f7db-4fa2-ab12-d3b247ffd641
# ‚ïü‚îÄ46596778-9dbd-426c-a2a7-4014b8b2987c
# ‚ïü‚îÄ086a03d5-aced-4ed5-a250-21ef3c51caeb
# ‚ïü‚îÄ6c85c8c1-8758-42ee-99a4-bfb81c70a966
